@echo off

rem opencon - launch the openconsole binary.
rem Runs the OpenConsole.exe binary generated by the build in the debug directory.
rem    Passes any args along.

if not exist %OPENCON%\bin\%ARCH%\%_LAST_BUILD_CONF%\OpenConsole.exe (
    echo Could not locate the OpenConsole.exe in %OPENCON%\bin\%ARCH%\%_LAST_BUILD_CONF%. Double check that it has been built and try again.
    goto :eof
)

setlocal
rem Generate a unique name, so that we can debug multiple revisions of the binary at the same time if needed.
set rand_val=%random%
set copy_name=OpenConsole\%rand_val%\OpenConsole.exe
set copy_propsheet_name=OpenConsole\%rand_val%\console.dll

(echo f | xcopy /Y %OPENCON%\bin\%ARCH%\%_LAST_BUILD_CONF%\OpenConsole.exe %TEMP%\%copy_name%) > nul
(echo f | xcopy /Y %OPENCON%\bin\%ARCH%\%_LAST_BUILD_CONF%\console.dll %TEMP%\%copy_propsheet_name%) > nul

start %TEMP%\%copy_name% %*
